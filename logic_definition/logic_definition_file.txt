program             = section_dev, section_con, section_mon;

section_dev         = "DEVICES", assignment, { assignment }, "END" ;
section_con         = "CONNECT", connection, { connection }, "END" ;
section_mon         = "MONITOR", monitor, { monitor }, "END" ;

assignment          = name, { "," , name }, "=", ( device_arg_dec | device ), ";" ; 
connection          = output, "->", input, { ",", input }, ";" ;
monitor  		    = ( input | output ), ";" ;

device_arg_dec      = device_arg, "(", argument , ")" ;
device              = "DTYPE" | "XOR" ;
device_arg          = "CLOCK" | "AND" | "NAND" | "OR" | "NOR" | "SWITCH" ;  

output              = name, [ ".", dtype_op_name ] ;
input               = name, ".", ( ( "I", argument ) | dtype_ip_name ) ;

argument            = digit , { digit } ;
name                = letter, { letter | digit } ;

dtype_ip_name       = "DATA" | "CLK" | "SET" | "CLEAR" ;
dtype_op_name       = "Q" | "QBAR" ;

comment             = "#" , { letter | digit | punctuation } ;