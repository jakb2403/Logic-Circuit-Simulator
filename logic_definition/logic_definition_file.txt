program             = section_dev, section_con, section_mon ;

section_dev         = "DEVICES", "{", assignment, { assignment }, "}" ;
section_con         = "CONNECT", "{", connection, { connection }, "}" ;
section_mon         = "MONITOR", "{", monitor, { monitor }, "}" ;

assignment          = name, { "," , name }, "=", ( device_arg_dec | device ), ";" ; 
connection          = ( output ), "->", ( input ), { "," , ( input ) }, ";" ;
monitor  		    = ( input | ouput ), ";" ;

device_arg_dec      = device_arg, "(", argument , ")" ;
device              = "DTYPE" | "XOR" ;
device_arg          = "CLOCK" | "AND" | "NAND" | "OR" | "NOR" | "SWITCH" ;  

input               = name, ".", ( "I", argument | dtype_ip_name ) ;
output              = name, [ "." , dtype_op_name ] ;
  
dtype_ip_name       = "DATA" | "CLK" | "SET" | "CLEAR" ;
dtype_op_name       = "Q" | "QBAR" ;

argument            = digit , { digit } ;
name                = letter, { letter | digit } ;